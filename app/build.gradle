apply plugin: 'com.android.application'
apply plugin: 'com.antfortune.freeline'
//ApkChannelPackage
apply plugin: 'channel'
//bugly tinker
//apply from: 'tinker-support.gradle'
//andResGuard
apply from: 'and-res-guard.gradle'

Properties properties = new Properties()
File localPropertiesFile = project.rootProject.file("local.properties");
if (localPropertiesFile.exists()) {
    properties.load(localPropertiesFile.newDataInputStream())
}

/**
 * 项目命令行打包指令：gradle resguardRelease rebuildChannel
 */
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        resConfigs rootProject.ext.android.resConfigs
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        testInstrumentationRunner rootProject.ext.dependencies["androidJUnitRunner"]

        multiDexEnabled true
        renderscriptTargetApi rootProject.ext.android.targetSdkVersion
        renderscriptSupportModeEnabled true
        /*ndk {
            abiFilters "armeabi"
        }*/
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
        //阿里用户反馈
        buildConfigField "String", "FEEDBACK_APP_KEY", properties.getProperty("fbAppKey")
        buildConfigField "String", "FEEDBACK_APP_SECRET", properties.getProperty("fbAppSecret")
        //bugly APP ID
        buildConfigField "String", "BUGLY_APP_ID", properties.getProperty("buglyAppId")
    }

    //gradle 3.0 以下
    /*android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                //这里修改apk文件名
                def fileName = "${defaultConfig.applicationId}_base_v${defaultConfig.versionName}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }*/

    //gradle 3.0
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${defaultConfig.applicationId}_base_v${defaultConfig.versionName}.apk"
        }
    }

    /*android.applicationVariants.all { variant ->
        //这个修改输出的APK路径
        if (variant.buildType.name != "debug") {//防止AS无法安装debug包(apk)
            variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apk")
        }
        variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
            //这个修改输出APK的文件名
            apkData.outputFileName = "${defaultConfig.applicationId}_base_v${defaultConfig.versionName}.apk"
        }
    }*/

    signingConfigs {
        rabtman {
            storeFile file("rabtman.jks")
            storePassword properties.getProperty("storePassword")
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "APP_DEBUG", "true"      //测试模式
            minifyEnabled false
            signingConfig signingConfigs.rabtman
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            buildConfigField "boolean", "APP_DEBUG", "false"
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            signingConfig signingConfigs.rabtman
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable "ResourceType"
        abortOnError false
    }
}

//渠道配置
channel {
    //多渠道包的输出目录，默认为new File(project.buildDir,"channel")
    baseOutputDir = new File(project.buildDir, "channel")
    //多渠道包的命名规则，默认为：${appName}-${versionName}-${versionCode}-${flavorName}-${buildType}
    apkNameFormat = 'com.rabtman.acgclub_${flavorName}_v${versionName}'
}

//rebuildChannel
//根据已有基础包重新生成多渠道包
rebuildChannel {
    //baseDebugApk = 已有Debug APK
    baseReleaseApk = new File(project.buildDir, "outputs/apk/com.rabtman.acgclub_base_v${rootProject.ext.android.versionName}.apk")
    //默认为new File(project.buildDir, "rebuildChannel/debug")
    //debugOutputDir = Debug渠道包输出目录
    //默认为new File(project.buildDir, "rebuildChannel/release")
    //releaseOutputDir = Release渠道包输出目录
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':common')
    implementation project(':component-acgnews')
    //di
    annotationProcessor rootProject.ext.dependencies["dagger2-compiler"]
    annotationProcessor rootProject.ext.dependencies["butterknife-compiler"]
    annotationProcessor rootProject.ext.dependencies["router-compiler"]
    //阿里用户反馈
    implementation 'com.aliyun.ams:alicloud-android-feedback:3.1.0@aar'
    implementation 'com.aliyun.ams:alicloud-android-utdid:1.1.5.3'
    implementation 'com.aliyun.ams:alicloud-android-ut:5.1.0'
    implementation 'com.aliyun.ams:alicloud-android-utils:1.0.3'
    //bugly
    implementation rootProject.ext.dependencies["crashreport"]
    implementation rootProject.ext.dependencies["nativecrashreport"]
    //ApkChannelPackage
    implementation rootProject.ext.dependencies["apk-channel-package"]
}
